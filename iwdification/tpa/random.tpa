/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// randomized equipment                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

INCLUDE ~iwdification/tpa/random_treasure.tpa~ // load functions

/////                                                  \\\\\
///// mix up generic helms                             \\\\\
/////                                                  \\\\\

// do this first as a check against file being present
ACTION_IF !FILE_EXISTS_IN_GAME rndtres.2da BEGIN

  COPY ~iwdification/2da/rndtres.2da~ ~override~ // seeded with cdrdhlm
  
END ELSE BEGIN  

  LAF cd_add_random_treasure STR_VAR // 20% chance to get nicer cosmetic helmet
        cd_random_item = ~cdrdhlm~
        cd_random_table = EVAL ~helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm01 helm08 helm09 helm10 helm11 helm12 helm13 helm15~ 
    RET cdrdhlm = cd_random_item_return
  END

END

ACTION_FOR_EACH cre IN 
  hobchil1 hobgbeg1 hobgbeg2 hobgbeg3 hobgbeg4 hobgbeg5 hobgbeg6 hobgbeg7 hobgbeg8 hobgoa_a hobgoa_b hobgoa_c hobgoa_d hobgoa_e
  hobgob hobgob_a hobgob_b hobgob_c hobgob_d hobgob_e hobgoba ogreha // bgee/sod
  arnwar02 bdturm01 bdturm04 flyfgt02 gororc02 hobarc01 hobarc02 hobcap01 hobsha01 hobwar01 kaypal03 obshal04 orc02 sensmsp2 
  yssold13 yssold15 // bg2ee
BEGIN
  LAF cd_match_random_profs STR_VAR old_item = helm01 new_item = cdrdhlm slot = helmet creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// add 2h axes to the mix, if installed             \\\\\
/////                                                  \\\\\

// add 2h axes to the mix if component installed
ACTION_IF !FILE_EXISTS_IN_GAME ~cdax2h1.itm~ BEGIN
  OUTER_SPRINT ~cdax2h1~    ~halb01~
  ACTION_IF !include_hq BEGIN
    OUTER_SPRINT ~cdax2h1_6~  ~halb01~ // for 1-in-6 hq replacement
    OUTER_SPRINT ~cdax2h1_8~  ~halb01~ // for 1-in-8 hq replacement
    OUTER_SPRINT ~cdax2h1_10~ ~halb01~ // for 1-in-10 hq replacement
  END ELSE BEGIN
    OUTER_SPRINT ~cdax2h1_6~  ~halb01 halb01 halb01 halb01 halb01 cdhqhalb~ // for 1-in-6 hq replacement
    OUTER_SPRINT ~cdax2h1_8~  ~halb01 halb01 halb01 halb01 halb01 halb01 halb01 cdhqhalb~ // for 1-in-8 hq replacement
    OUTER_SPRINT ~cdax2h1_10~ ~halb01 halb01 halb01 halb01 halb01 halb01 halb01 halb01 halb01 cdhqhalb~ // for 1-in-10 hq replacement
  END  
END ELSE BEGIN  
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_make_hq_items BEGIN cdax2h1,15,0,15 => cdhqax2h END // 2h axe
  OUTER_SPRINT ~cdax2h1~  ~cdax2h1~
END  

/////                                                  \\\\\
///// create HQ versions of items                      \\\\\
/////                                                  \\\\\

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_make_hq_items BEGIN // ^^ 2h axe added to the array, above, if installed
  // source item, color loc x3 (16,20,21) => hq resref - two letter code in comment is used for fake weapons (2h axe is 2a)
  arow01,   0,  0,   0 => cdhqarow // arrows
  ax1h01,  15,  0,  15 => cdhqaxe  // ax axe
  blun01,   0,  0,   0 => cdhqclub // cl club
  blun02,  15,  0,  15 => cdhqflal // fl flail
  blun04, 103,  0, 103 => cdhqmace // mc mace
  blun06,   7,  0,  15 => cdhqmstr // ms morning star
  bolt01,   0,  0,   0 => cdhqbolt // bolts
  bow01,    0,  0,   0 => cdhqcbow // cw composite long bow 
  bow03,    0,  0,   0 => cdhqlbow // bw long bow
  bow05,    0,  0,   0 => cdhqsbow // bs short bow
  bull01,   0,  0,   0 => cdhqbull // bullets
  dagg01,   0,  0,  15 => cdhqdagg // dd dagger
  halb01,  15,  0,  15 => cdhqhalb // hb halberd
  hamm01,   0,  7,  39 => cdhqhamm // wh hammer
  sper01,   0,  0,  15 => cdhqsper // sp spear
  staf01,   0,  0,   0 => cdhqstaf // qs staff
  sw1h01,   0,  0,  15 => cdhqsw1b // s0 bastard sword
  sw1h04,   0,  0,  15 => cdhqsw1l // s1 long sword
  sw1h07,  15,  0,   0 => cdhqsw1s // ss short sword
  sw1h20,   0,  0,  15 => cdhqscim // sc scimitar
  sw1h43,   0,  0,  15 => cdhqkat  // s3 katana
  sw1h46,   0,  0,  15 => cdhqwaki // wk wakizashia
  sw1h48,   0,  0,  15 => cdhqninj // nt ninja-to
  sw2h01,   0,  0,  15 => cdhqsw2h // s2 two-handed swords
  xbow01,   0,  0,   0 => cdhqcbhv // cb heavy crossbow
  xbow04,   0,  0,  15 => cdhqcblt // lc light crossbow
  
END

ACTION_IF !include_hq BEGIN // if no hq items, their variables will evaluate to the normal item
 
  ACTION_PHP_EACH cd_make_hq_items AS source => newitem BEGIN

    OUTER_SPRINT ~%source_0%_6~  ~%source_0%~ // for 1-in-6 hq replacement
    OUTER_SPRINT ~%source_0%_8~  ~%source_0%~ // for 1-in-8 hq replacement
    OUTER_SPRINT ~%source_0%_10~ ~%source_0%~ // for 1-in-10 hq replacement
    
  END

END ELSE BEGIN // if hq items, create them and set their variables to their resrefs 

  OUTER_SPRINT name_prefix @14003
  OUTER_SPRINT name_suffix @14004
  OUTER_SPRINT desc_new    @14005
  OUTER_SPRINT desc_stats  @14006
  OUTER_SPRINT desc_thac0  @14007

  ACTION_PHP_EACH cd_make_hq_items AS source => newitem BEGIN

    OUTER_SPRINT ~%source_0%_6~  ~%source_0% %source_0% %source_0% %source_0% %source_0% %newitem%~ // for 1-in-6 hq replacement
    OUTER_SPRINT ~%source_0%_8~  ~%source_0% %source_0% %source_0% %source_0% %source_0% %source_0% %source_0% %newitem%~ // for 1-in-8 hq replacement
    OUTER_SPRINT ~%source_0%_10~ ~%source_0% %source_0% %source_0% %source_0% %source_0% %source_0% %source_0% %source_0% %source_0% %newitem%~ // for 1-in-10 hq replacement

    COPY ~iwdification/bam/%newitem%.bam~ ~override~
    LAF cd_make_hq_weapon STR_VAR base_item = EVAL ~%source_0%~ hq_item = EVAL ~%newitem%~ END
    
  END  

/////                                                  \\\\\
///// mix in the occassional high quality arrows       \\\\\
/////                                                  \\\\\

  LAF cd_add_random_treasure STR_VAR
        cd_random_item = ~cdrdarow~
        cd_random_table = EVAL ~%arow01_10%~ 
    RET cdrdarow = cd_random_item_return
  END
  
  ACTION_FOR_EACH cre IN 
    bandit bandita bdhobg02 bounty flampun2 hobchil2 hobelite hobgbeg1 hobgbeg2 hobgbeg3 hobgbeg4 hobgbeg9 
    hobgoa_a hobgoa_b hobgoa_c hobgoa_d hobgoa_e hobgoba ironelit kobcap01 kobola_a kobola_b kobola_c 
    kobola_d kobola_e kobolda orc02 skelet_c // bgee/sod
    arnwar01 bdturm04 bodfgt02 flyfgt03 hobarc01 hobeli01 icgob01 kaysmg03 kobarc01 kobcom01 ohnrwme2 
    rorca01 skelwa03 ysgrunt yssold12 // bg2ee, omitted as it's in bgee list: kobcap01
  BEGIN
    LAF cd_match_random_profs STR_VAR old_item = arow01 new_item = cdrdarow slot = quiver creature = EVAL ~%cre%~ END
  END

/////                                                  \\\\\
///// mix in the occassional high quality bolts        \\\\\
/////                                                  \\\\\

  LAF cd_add_random_treasure STR_VAR 
        cd_random_item = ~cdrdbolt~
        cd_random_table = EVAL ~%bolt01_10%~ 
    RET cdrdbolt = cd_random_item_return
  END
  
  ACTION_FOR_EACH cre IN 
    hgwar01 kuowhi01 orc06 ppthf02 rerak05 rerak06 obssah03 obssah04 sahbar01 sahbar02 sahramb1 sahramb2 
    sahramb4 sahramb5 sahrgr01 sahrgr02 sahsss01 sahsss02 sahsss03 sahuag01 spmugg spmugg2 spmugg3 // bg2ee
  BEGIN
    LAF cd_match_random_profs STR_VAR old_item = arow01 new_item = cdrdbolt slot = quiver creature = EVAL ~%cre%~ END
  END

/////                                                  \\\\\
///// mix in the occassional high quality 2h sword     \\\\\
/////                                                  \\\\\

  LAF cd_add_random_treasure STR_VAR 
        cd_random_item = ~cdrdsw2h~
        cd_random_table = EVAL ~%sw2h01_10%~ 
    RET cdrdsw2h = cd_random_item_return
  END
  
  ACTION_FOR_EACH cre IN 
    skelwa03 // bg2ee
  BEGIN
    LAF cd_match_random_profs STR_VAR old_item = sw2h01 new_item = cdrdsw2h slot = weapon creature = EVAL ~%cre%~ END
  END

/////                                                  \\\\\
///// mix in the occassional high quality staff        \\\\\
/////                                                  \\\\\

  LAF cd_add_random_treasure STR_VAR 
        cd_random_item = ~cdrdstaf~
        cd_random_table = EVAL ~%staf01_10%~ 
    RET cdrdstaf = cd_random_item_return
  END
  
  ACTION_FOR_EACH cre IN 
    drow06 orc05 // bg2ee
  BEGIN
    LAF cd_match_random_profs STR_VAR old_item = staf01 new_item = cdrdstaf slot = weapon creature = EVAL ~%cre%~ END
  END

/////                                                  \\\\\
///// mix in the occassional high quality axe          \\\\\
/////                                                  \\\\\

  LAF cd_add_random_treasure STR_VAR 
        cd_random_item = ~cdrdaxe~
        cd_random_table = EVAL ~%ax1h01_10%~ 
    RET cdrdaxe = cd_random_item_return
  END
  
  ACTION_FOR_EACH cre IN 
    icmin01 // bg2ee
  BEGIN
    LAF cd_match_random_profs STR_VAR old_item = ax1h01 new_item = cdrdaxe slot = weapon creature = EVAL ~%cre%~ END
  END
  
/////                                                  \\\\\
///// mix in hq light crossbows for thieves            \\\\\
/////                                                  \\\\\

  LAF cd_add_random_treasure STR_VAR
        cd_random_item = ~cdrdcbth~
        cd_random_table = EVAL ~%xbow04_10%~ 
    RET cdrdcbth = cd_random_item_return
  END
  
  ACTION_FOR_EACH cre IN 
    bdturm02 // bg2ee
  BEGIN
    LAF cd_match_random_profs STR_VAR old_item = xbow04 new_item = cdrdcbth slot = weapon creature = EVAL ~%cre%~ END
  END

/////                                                  \\\\\
///// mix in the occassional hq composite long bow     \\\\\
/////                                                  \\\\\

  LAF cd_add_random_treasure STR_VAR 
        cd_random_item = ~cdrdclbw~
        cd_random_table = EVAL ~%bow01_10%~ 
    RET cdrdclbw = cd_random_item_return
  END
  
  ACTION_FOR_EACH cre IN 
    bounty flampun2 kobcap01 orc02 skelet_c // bgee/sod   
    kobarc01 ohnrwme2 refigh01 rethie01 skelwa03 tantug03 ysgrunt yssold12 // bg2ee; kobcap01 is repeat
  BEGIN
    LAF cd_match_random_profs STR_VAR old_item = bow01 new_item = cdrdclbw slot = weapon creature = EVAL ~%cre%~ END
  END

/////                                                  \\\\\
///// mix in the occassional hq short bow              \\\\\
/////                                                  \\\\\

// primarily meant for thieves; have another replace down below that also mixes in long bows
  LAF cd_add_random_treasure STR_VAR 
        cd_random_item = ~cdrdsbth~
        cd_random_table = EVAL ~%bow05_10%~ 
    RET cdrdsbth = cd_random_item_return
  END
  
  ACTION_FOR_EACH cre IN 
    kaysmg03 // bg2ee
  BEGIN
    LAF cd_match_random_profs STR_VAR old_item = bow05 new_item = cdrdsbth slot = weapon creature = EVAL ~%cre%~ END
  END

/////                                                  \\\\\
///// mix in the occassional hq heavy crossbow         \\\\\
/////                                                  \\\\\

  LAF cd_add_random_treasure STR_VAR
        cd_random_item = ~cdrdcbhv~
        cd_random_table = EVAL ~%xbow01_10%~ 
    RET cdrdcbhv = cd_random_item_return
  END
  
  ACTION_FOR_EACH cre IN 
    kuowhi01 ppthf02 rerak05 rerak06 sahramb4 sahramb5 sahrgr02 sahsss01 sahsss03 sahuag01 // bg2ee  
  BEGIN
    LAF cd_match_random_profs STR_VAR old_item = xbow01 new_item = cdrdcbhv slot = weapon creature = EVAL ~%cre%~ END
  END

/////                                                  \\\\\
///// mix in the occassional hq axe                    \\\\\
/////                                                  \\\\\

// for the goblin w/axe animation, as an axe & shield is hardcoded to the animation
  LAF cd_add_random_treasure STR_VAR
        cd_random_item = ~cdrdgaxe~
        cd_random_table = EVAL ~%ax1h01_10%~ 
    RET cdrdgaxe = cd_random_item_return
  END
  
  ACTION_FOR_EACH cre IN
    icgob03 sargrd04 // bg2ee  
  BEGIN
    LAF cd_match_random_profs STR_VAR old_item = ax1h01 new_item = cdrdgaxe slot = weapon creature = EVAL ~%cre%~ END
  END
 
END 

/////                                                  \\\\\
///// make cosmetic items                              \\\\\
/////                                                  \\\\\ 

OUTER_SPRINT base_item sw1h01    // bastard sword w/ long sword animation
OUTER_SPRINT hq_item   cdhqsw1b
OUTER_SPRINT fake_item s0
OUTER_SPRINT animation S1
REINCLUDE ~iwdification/tpa/random_make_fake.tpa~ // why no function? I need to return three variables

OUTER_SPRINT base_item sw1h07    // short sword w/ long sword animation
OUTER_SPRINT hq_item   cdhqsw1s
OUTER_SPRINT fake_item ss
REINCLUDE ~iwdification/tpa/random_make_fake.tpa~

OUTER_SPRINT base_item sw1h20    // scimitar w/ long sword animation
OUTER_SPRINT hq_item   cdhqscim
OUTER_SPRINT fake_item sc
REINCLUDE ~iwdification/tpa/random_make_fake.tpa~ 

OUTER_SPRINT base_item sw1h04    // long sword w/ short sword animation
OUTER_SPRINT hq_item   cdhqsw1l
OUTER_SPRINT fake_item s1
OUTER_SPRINT animation SS
REINCLUDE ~iwdification/tpa/random_make_fake.tpa~

OUTER_SPRINT base_item bow05     // short bow w/ long bow animation
OUTER_SPRINT hq_item   cdhqsbow
OUTER_SPRINT fake_item bs
OUTER_SPRINT animation BW
REINCLUDE ~iwdification/tpa/random_make_fake.tpa~

/////                                                  \\\\\
///// 10% chance for leather > studded leather         \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR 
      cd_random_item = ~cdrdlt1~
      cd_random_table = EVAL ~leat01 leat01 leat01 leat01 leat01 leat01 leat01 leat01 leat01 leat04~ 
  RET cdrdlt1 = cd_random_item_return
END
  
ACTION_FOR_EACH cre IN 
  // bgee/sod
  bandit bandita bounty hobchil1 hobchil2 hobelite hobgbeg1 hobgbeg3 hobgbeg4 
  hobgbeg5 hobgbeg7 hobgbeg8 hobgbeg9 hobgoa_a hobgoa_b hobgoa_d hobgoa_e hobgob 
  hobgob_a hobgob_b hobgob_d hobgob_e hobgoba //orc02 in bg2ee list, next
  bdturm04 genth01 genth02 gororc01 gororc02 hobarc01 hobarc02 hobeli01 hobsha01 hobwar01 kaysmg02 kaysmg03 
  kaysmg04 orc01 orc02 orc05 orc06 ppsail01 ppsail02 ppsail03 reband01 reband02 reband03 rethug01 rethug03 
  rethug04 rorca01 rorcs01 tanthf01 // bg2ee
BEGIN
  LAF cd_match_random_profs STR_VAR old_item = leat01 new_item = cdrdlt1 slot = armor creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// 10% chance for studded leather > hide            \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR 
      cd_random_item = ~cdrdlt4~
      cd_random_table = EVAL ~leat04 leat04 leat04 leat04 leat04 leat04 leat04 leat04 leat04 leat10~ 
  RET cdrdlt4 = cd_random_item_return
END
  
ACTION_FOR_EACH cre IN 
  flamsco hobgbeg2 hobgbeg6 hobgoa_c hobgob_c orc01 // bgee/sod
  hobsha01 ppthf02 spmugg spmugg2 spmugg3 // bg2ee
  arnwar03
BEGIN
  LAF cd_match_random_profs STR_VAR old_item = leat04 new_item = cdrdlt4 slot = armor creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// 10% chance for chain > splint                    \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR 
      cd_random_item = ~cdrdch1~
      cd_random_table = EVAL ~chan01 chan01 chan01 chan01 chan01 chan01 chan01 chan01 chan01 chan04~ 
  RET cdrdch1 = cd_random_item_return
END
  
ACTION_FOR_EACH cre IN 
  arnwar01 c6elf2 flyfgt03 obshal04 ppthf01 reband05 yssold16 // bg2ee
BEGIN
  LAF cd_match_random_profs STR_VAR old_item = chan01 new_item = cdrdch1 slot = armor creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// mix up bucklers (cosmetic)                       \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR 
      cd_random_item = ~cdrdshb~
      cd_random_table = EVAL ~shld08 shld09 shld10~ 
  RET cdrdshb = cd_random_item_return
END
  
ACTION_FOR_EACH cre IN 
  bdskgr00 skelet_a skelet_b // bgee/sod
BEGIN
  LAF cd_match_random_profs STR_VAR new_item = cdrdshb slot = offhand creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// mix up small shields (cosmetic)                  \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR 
      cd_random_item = ~cdrdshs~
      cd_random_table = EVAL ~shld01 shld11 shld12~ 
  RET cdrdshs = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  flampun skelet03 // bgee/sod
  arnwar02 flyfgt02 rorcl01 // bg2ee
BEGIN
  LAF cd_match_random_profs STR_VAR new_item = cdrdshs slot = offhand creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// mix up medium shields (cosmetic)                 \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR 
      cd_random_item = ~cdrdshm~
      cd_random_table = EVAL ~shld03 shld13 shld14~ 
  RET cdrdshm = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  irongu skelets // bgee/sod
  hobcap01 // bg2ee
BEGIN
  LAF cd_match_random_profs STR_VAR new_item = cdrdshm slot = offhand creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// mix up large shields (cosmetic)                  \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR 
      cd_random_item = ~cdrdshl~
      cd_random_table = EVAL ~shld05 shld15 shld16~ 
  RET cdrdshl = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  yssold13 // bg2ee
BEGIN
  LAF cd_match_random_profs STR_VAR new_item = cdrdshl slot = offhand creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// cold arrow randomization for black talon elites  \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR 
      cd_random_item = ~cdrdbte~
      cd_random_table = EVAL ~arow04 arow05 arow08 arow09 arow10~ // acid, biting, fire, cold, piercing
  RET cdrdbte = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  ironelit // bgee/sod
BEGIN
  LAF cd_match_random_profs STR_VAR old_item = arow09 new_item = cdrdbte slot = quiver creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// mix in long swords and HQ weapons for xvarts     \\\\\
/////                                                  \\\\\

// xvart animation uses hardcoded sword animation; long sword is stretching it a bit but what the hell
LAF cd_add_random_treasure STR_VAR
      cd_random_item = ~cdrdxvrt~
      cd_random_table = EVAL ~%sw1h07_8% %sw1h07_8% %sw1h07_8% %sw1h07_8% %sw1h04_8%~ // 25% long sword/75% short sword; 1-in-8 HQ
  RET cdrdxvrt = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  xvart xvart_a xvart_b xvart_c // bgee/sod
BEGIN
  LAF cd_match_random_profs STR_VAR old_item = sw1h07 new_item = cdrdxvrt slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// gnoll halberd replacements                       \\\\\
/////                                                  \\\\\

// gnoll animation allows for long swords and halberds; change to 60% halberd, 20% 2h axe (halberd if not installed), 20% long sword; 1-in-8 for high-quality in each 
LAF cd_add_random_treasure STR_VAR
      cd_random_item  = ~cdrdgnl~
      cd_random_table = EVAL ~%halb01_8% %halb01_8% %halb01_8% %cdax2h1_8% %sw1h04_8%~
  RET cdrdgnl = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  gnoll gnoll_a gnoll_b gnoll_d gnoll_e // bgee/sod
  gnlcap01 gnleli01 gnlsla01 gnlvet01 gnlwar01 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 99 prof_lsword = 1 prof_ssword = 1 prof_spear = 2 
                            STR_VAR old_item = halb01 new_item = cdrdgnl slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// sahuagins spear replacements                     \\\\\
/////                                                  \\\\\

// sahuagin support spear and staff animations: 75% spear, 25% staff; 10% hq
LAF cd_add_random_treasure STR_VAR 
      cd_random_item  = ~cdrdsah~
      cd_random_table = EVAL ~%sper01_10% %sper01_10% %sper01_10% %staf01_10%~ 
  RET cdrdsah = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
 obssah02 obssah03 obssah04 obssah05 sahbar02 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 98 prof_blunt = 1 prof_spear = 2 
                            STR_VAR old_item = sper01 new_item = cdrdsah slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// half-ogre, orc replacements                      \\\\\
/////                                                  \\\\\

// half-ogre and orc-melee have hardcoded sword animations, change to 40% bastard 20% each for long, short, and scimitar; 20% for high-quality in each
LAF cd_add_random_treasure STR_VAR
    cd_random_item  = ~cdrdhogr~
    cd_random_table = EVAL ~%sw1h01_8% %sw1h01_8% %sw1h04_8% %sw1h07_8% %sw1h20_8%~ 
  RET cdrdhogr = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  ogreha // bgee/sod
  orc01 orc06 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 89 prof_lsword = 2 prof_ssword = 1 
                            STR_VAR old_item = sw1h01 new_item = cdrdhogr slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// upgrade lbows to the occassional composite &  hq \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR 
    cd_random_item = ~cdrdlbw~
    cd_random_table = EVAL ~%bow03_8% %bow03_8% %bow03_8% %bow03_8% %bow01_8%~ // 80% long bow/20% composite long bow; 1-in-8 HQ
  RET cdrdlbw = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  bandita bdhobg02 ironelit // bgee/sod
  gororc02 hobarc02 orc02 skelar01 // bg2ee
BEGIN
  LAF cd_match_random_profs STR_VAR old_item = bow03 new_item = cdrdlbw slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// upgrade lt xbows to the occassional heavy &  hq  \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR
    cd_random_item = ~cdrdcblt~
    cd_random_table = EVAL ~%xbow04_8% %xbow04_8% %xbow04_8% %xbow04_8% %xbow01_8%~ // 80% lt crossbow/20% hv crossbow; 1-in-8 HQ
  RET cdrdcblt = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  kuocap01 kuolie01 kuopri01 kuowar01 obssah03 obssah04 obssah05 sahbar01 sahbar02 
  sahramb1 sahramb2  sahrgr01 sahsss02 spmugg spmugg2 spmugg3 // bg2ee 
BEGIN
  LAF cd_match_random_profs STR_VAR old_item = xbow04 new_item = cdrdcblt slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// upgrade shortbows to the occassional long &  hq  \\\\\
/////                                                  \\\\\

LAF cd_add_random_treasure STR_VAR
    cd_random_item = ~cdrdsbow~
    cd_random_table = EVAL ~%bow05_8% %bow05_8% %bow05_8% %bow05_8% %bow03_8%~ // 80% short bow/20% long bow; 1-in-8 HQ
  RET cdrdsbow = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  arnwar01 bdturm04 bodfgt02 flyfgt03 icgob01 rorca01 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 105 prof_bow = 2 
                            STR_VAR old_item = bow05 new_item = cdrdsbow slot = weapon creature = EVAL ~%cre%~ END
END

// one fix: 1pp gave a comp weapon to a creature with an animation that supports the original weapon
LAF cd_match_random_profs STR_VAR old_item = bow05 new_item = cdrdsbow comp_item = compb05 slot = weapon creature = bandit END

/////                                                  \\\\\
///// randomize lizard man weapons                     \\\\\
/////                                                  \\\\\

// lizadman animation supports staves, halberds: 60% halberd, 20% staff, 20% 2h axe (sub halberd); 20% HQ
LAF cd_add_random_treasure STR_VAR
      cd_random_item = ~cdrdlman~
      cd_random_table = EVAL ~%halb01_8% %halb01_8% %halb01_8% %cdax2h1_8% %staf01_8%~ 
  RET cdrdlman = cd_random_item_return
END

ACTION_FOR_EACH cre IN
  icliz01 // bg2ee  
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 99 prof_spear = 2 prof_axe = 1 prof_blunt = 1 
                            STR_VAR old_item = halb01 new_item = cdrdlman slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// randomize yuan-ti weapons                        \\\\\
/////                                                  \\\\\

// yuan-ti support sword, axe: equal shots at 2h/bastard/long/short swords, scimitars, axe; 1-in-6 HQ
LAF cd_add_random_treasure STR_VAR 
      cd_random_item = ~cdrdyuan~
      cd_random_table = EVAL ~%ax1h01_6% %sw1h01_6% %sw1h04_6% %sw1h07_6% %sw1h20_6% %sw2h01_6%~ 
  RET cdrdyuan = cd_random_item_return
END

ACTION_FOR_EACH cre IN
  icyuan01 icyuan02 kpyuan01 // bg2ee  
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 90 prof_lsword = 2 prof_axe = 1 prof_ssword = 1 
                            STR_VAR old_item = sw1h04 new_item = cdrdyuan slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// one-handed melee blunt weapons                   \\\\\
/////                                                  \\\\\

// for skeleton/character animations that support all weapons, really mix it up
LAF cd_add_random_treasure STR_VAR
      cd_random_item = ~cdrd1hmb~
      cd_random_table = EVAL ~%blun01_8% %blun02_8% %blun04_8% %blun06_8% %hamm01_8%~ // equal between club, mace, hammer, flail, morningstar; 1-in-8 HQ
  RET cdrd1hmb = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  skelet_b /* bgee/sod */ obshal04 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 100 prof_spiked = 2 prof_blunt = 1 // blun06 to 1-handed melee, blunt 
                            STR_VAR old_item = blun06 new_item = cdrd1hmb slot = weapon creature = EVAL ~%cre%~ END
END

ACTION_FOR_EACH cre IN 
  flampun flampun2 // bgee/sod
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 100 prof_spiked = 2 prof_blunt = 1 // blun06 to 1-handed melee, blunt 
                            STR_VAR old_item = blun02 new_item = cdrd1hmb slot = weapon creature = EVAL ~%cre%~ END
END

ACTION_FOR_EACH cre IN 
  rethug03 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 91 prof_ssword = 2 prof_spiked = 1 prof_blunt = 1 // sw1h07 to 1-handed melee, blunt (is cleric)
                            STR_VAR old_item = sw1h07 new_item = cdrd1hmb slot = weapon creature = EVAL ~%cre%~ END
END

ACTION_FOR_EACH cre IN 
  reband05 yssold16 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 115 prof_spiked = 1 prof_blunt = 2 // blun01 to 1-handed melee, blunt
                            STR_VAR old_item = blun01 new_item = cdrd1hmb slot = weapon creature = EVAL ~%cre%~ END
END

ACTION_FOR_EACH cre IN 
  kaysmg04 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 101 prof_spiked = 1 prof_blunt = 2 // blun04 to 1-handed melee, blunt
                            STR_VAR old_item = blun04 new_item = cdrd1hmb slot = weapon creature = EVAL ~%cre%~ END
END

ACTION_FOR_EACH cre IN 
  skelet_a bdskgr00 // bgee/sod
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 97 prof_blunt = 2 prof_spiked = 1 // hamm01 to 1-handed melee, blunt
                            STR_VAR old_item = hamm01 new_item = cdrd1hmb slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// one-handed melee thief weapons                   \\\\\
/////                                                  \\\\\

// for skeleton/character animations that support all weapons, really mix it up - but limited in this case for thieves
LAF cd_add_random_treasure STR_VAR
      cd_random_item = ~cdrdth1m~
      cd_random_table = EVAL ~%blun01_8% %dagg01_8% %sw1h04_8% %sw1h07_8% %sw1h20_8%~ // equal club, dagger, long sword, short sword, scimitar; 1-in-8 HQ
  RET cdrdth1m = cd_random_item_return
END

ACTION_FOR_EACH cre IN  // long sword to one-handed thief melee
  flyfgt02 flyfgt03 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 90 prof_lsword = 2 prof_blunt = 1 prof_ssword = 1
                            STR_VAR old_item = sw1h04 new_item = cdrdth1m slot = weapon creature = EVAL ~%cre%~ END
END  

ACTION_FOR_EACH cre IN  // short sword to one-handed thief melee
  arnwar03 flyfgt01 gentmg01 genth01 reband01 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 91 prof_lsword = 1 prof_blunt = 1 prof_ssword = 2 
                            STR_VAR old_item = sw1h07 new_item = cdrdth1m slot = weapon creature = EVAL ~%cre%~ END
END    
ACTION_FOR_EACH cre IN  // dagger to one-handed thief melee
  genth02 kaysmg03 reband02 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 91 prof_lsword = 1 prof_blunt = 1 prof_ssword = 2 // dagger to one-handed thief melee
                            STR_VAR old_item = dagg01 new_item = cdrdth1m slot = weapon creature = EVAL ~%cre%~ END
END                    
LAF cd_match_random_profs INT_VAR old_bg2_prof = 91 prof_lsword = 2 prof_blunt = 1 prof_ssword = 1 // scimitar to one-handed thief melee
                          STR_VAR old_item = sw1h20 new_item = cdrdth1m slot = weapon creature = ppthf02 END
             
/////                                                  \\\\\
///// one-handed melee bladed weapons                  \\\\\
/////                                                  \\\\\    
         
// for skeleton/character animations that support all weapons, really mix it up 
LAF cd_add_random_treasure STR_VAR
      cd_random_item = ~cdrd1hms~
      cd_random_table = EVAL ~%ax1h01_6% %dagg01_6% %sw1h01_6% %sw1h04_6% %sw1h07_6% %sw1h20_6%~ 
  RET cdrd1hms = cd_random_item_return
END

ACTION_FOR_EACH cre IN  // short sword to 1-handed melee, bladed
  flamsco skelets // bgee/sod
  rethug01 rethug04 spmugg spmugg2 spmugg3 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 91 prof_lsword = 1 prof_axe = 1 prof_ssword = 2 
                            STR_VAR old_item = sw1h07 new_item = cdrd1hms slot = weapon creature = EVAL ~%cre%~ END
END                          
ACTION_FOR_EACH cre IN  // long sword to 1-handed melee, bladed
  bandit bandita bounty irongu ironelit // bgee/sod
  arnwar01 reband03 skelet03 rorca01 yssold12 yssold13 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 90 prof_lsword = 2 prof_axe = 1 prof_ssword = 1 
                            STR_VAR old_item = sw1h04 new_item = cdrd1hms slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// one-handed throwing weapons                      \\\\\
/////                                                  \\\\\    
         
// for skeleton/character animations that support all weapons
LAF cd_add_random_treasure STR_VAR
      cd_random_item = ~cdrd1hth~
      cd_random_table = EVAL ~dart01 dagg05~ 
  RET cdrd1hth = cd_random_item_return
END

LAF cd_match_random_profs INT_VAR old_bg2_prof = 106 prof_missile = 2 prof_ssword = 1 // dart to 1-handed throwing
                          STR_VAR old_item = dart01 new_item = cdrd1hth slot = weapon creature = skelet_b END
LAF cd_match_random_profs INT_VAR old_bg2_prof = 96 prof_missile = 1 prof_ssword = 2 // dagg05 to 1-handed throwing
                          STR_VAR old_item = dagg05 new_item = cdrd1hth slot = weapon creature = skelets END
             
/////                                                  \\\\\
///// two-handed melee weapons                         \\\\\
/////                                                  \\\\\    
         
// for skeleton/character animations that support all weapons, really mix it up
ACTION_IF !FILE_EXISTS_IN_GAME ~cdax2h1.itm~ BEGIN 
  OUTER_SPRINT ~2hweps~ ~%halb01_10% %sper01_10% %staf01_10% %sw2h01_10%~
END ELSE BEGIN  
  OUTER_SPRINT ~2hweps~ EVAL ~%halb01_8% %sper01_8% %staf01_8% %sw2h01_8% %cdax2h1_8%~
END

LAF cd_add_random_treasure STR_VAR
    cd_random_item = ~cdrd2hm~
    cd_random_table = EVAL ~%2hweps%~
  RET cdrd2hm = cd_random_item_return
END

ACTION_FOR_EACH cre IN // 2-handed sword to 2-handed melee
  skelet_c // bgee/sod
  gromg14 plshkn02 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 99 prof_lsword = 1 prof_blunt = 1 prof_spear = 2 prof_axe = 1 // halberd to 2-handed melee
                            STR_VAR old_item = halb01 new_item = cdrd2hm slot = weapon creature = EVAL ~%cre%~ END  
END                          
ACTION_FOR_EACH cre IN // 2-handed sword to 2-handed melee
  kaypal03 kaysmg02 ohnrwme1 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 93 prof_lsword = 2 prof_blunt = 1 prof_spear = 1 prof_axe = 1
                            STR_VAR old_item = sw2h01 new_item = cdrd1hms slot = weapon creature = EVAL ~%cre%~ END
END
                          
ACTION_FOR_EACH cre IN // spear to 2-handed melee
  ohnrwme2 plshkn01 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 98 prof_lsword = 1 prof_blunt = 1 prof_spear = 2 prof_axe = 1
                            STR_VAR old_item = sper01 new_item = cdrd1hms slot = weapon creature = EVAL ~%cre%~ END
END

/////                                                  \\\\\
///// kobold weapon mix                                \\\\\
/////                                                  \\\\\ 

// mix in 
LAF cd_add_random_treasure STR_VAR 
    cd_random_item = ~cdrdkob~
    cd_random_table = EVAL ~%sw1h07_10% %sw1h07_10% %sw1h07_10% %cdfs1SS_10%~ 
  RET cdrdkob = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  // bgee/sod
  kobcap01 kobcomm kobola_a kobola_b kobola_c kobola_d kobola_e kobold kobold_a kobold_b kobold_c
  kobold_d kobold_e kobolda 
  kobarc01 kobcom01 kobwar01 // bg2ee; kobcap01 is repeat
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 91 prof_lsword = 1 prof_ssword = 2
                            STR_VAR old_item = sw1h07 new_item = cdrdkob slot = weapon creature = EVAL ~%cre%~ END
END
             
/////                                                  \\\\\
///// hobgoblin melee mix                              \\\\\
/////                                                  \\\\\ 

LAF cd_add_random_treasure STR_VAR // equal chances scimitar, bastard/long/short sword; 1-in-10 for HQ
    cd_random_item = ~cdrdhobm~
    cd_random_table = EVAL ~%cdfs0S1_10% %sw1h04_10% %cdfssS1_10% %cdfscS1_10%~ 
  RET cdrdhobm = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  bdhobg02 bdhobg05 // bgee/sod
  ppthf01 hobcap01 rakmah01 rakraj01 rakruh01 raksha01 rerak01 rerak02 rerak03 rerak04 rerak05 rerak06 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 90 prof_lsword = 2 prof_ssword = 1
                            STR_VAR old_item = sw1h04 new_item = cdrdhobm slot = weapon creature = EVAL ~%cre%~ END
END
             
/////                                                  \\\\\
///// compb05 mix                                      \\\\\
/////                                                  \\\\\ 

// compb05 is a short bow with a long bow animation
// this is for creatures using short bows but only have long bow animations; namely sirines, hobgoblins and kobolds
LAF cd_add_random_treasure STR_VAR // 75% short bow, 25% long bow; 1-in-10 for HQ
    cd_random_item = ~cdrdcb5~
    cd_random_table = EVAL ~%cdfbsBW_10% %cdfbsBW_10% %cdfbsBW_10% %bow03_10%~ 
  RET cdrdcb5 = cd_random_item_return
END

ACTION_FOR_EACH cre IN 
  hobchil2 hobelite hobgbeg1 hobgbeg2 hobgbeg3 hobgbeg4 hobgbeg9 hobgoa_a hobgoa_b hobgoa_c hobgoa_d
  hobgoa_e hobgoba kobcomm kobola_a kobola_b kobola_c kobola_d kobola_e kobolda // bgee/sod
  chevil01 hobarc01 hobeli01 kobcom01 sirine01 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 105 prof_bow = 2 
                            STR_VAR old_item = bow05 new_item = cdrdcb5 comp_item = compb05 slot = weapon creature = EVAL ~%cre%~ END
END          
             
/////                                                  \\\\\
///// comps01 mix                                      \\\\\
/////                                                  \\\\\ 

// comps01 is a bastard sword with a long sword animation
// this is for creatures using bastard swords but only have long sword animations; namely hobgoblins and ogre mages
LAF cd_add_random_treasure STR_VAR // 60% bastard sword, 20% long sword, 20% scimitar; 1-in-8 HQ
    cd_random_item = ~cdrdcs1~
    cd_random_table = EVAL ~%cdfs0S1_8% %cdfs0S1_8% %cdfs0S1_8% %cdfscS1_8% %sw1h04_8%~ 
  RET cdrdcs1 = cd_random_item_return
END

ACTION_FOR_EACH cre IN // creatures with bastard swords
  hobchil1 hobchil2 hobelite hobgbeg1 hobgbeg2 hobgbeg3 hobgbeg4 hobgbeg5 hobgbeg6 hobgbeg7 hobgbeg8 hobgbeg9 hobgoa_a 
  hobgoa_b hobgoa_c hobgoa_d hobgoa_e hobgob hobgob_a hobgob_b hobgob_c hobgob_d hobgob_e hobgoba // bgee/sod
  chevil03 hobarc01 hobarc02 hobarc02 hobeli01 hobwar01 plshom01 // bg2ee
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 89 prof_lsword = 2
                            STR_VAR old_item = sw1h01 new_item = cdrdcs1 comp_item = comps01 slot = weapon creature = EVAL ~%cre%~ END
END

ACTION_FOR_EACH cre IN  // creatures with scimitars
  ogrema // bgee/sod
BEGIN
  LAF cd_match_random_profs INT_VAR old_bg2_prof = 89 prof_lsword = 2
                            STR_VAR old_item = sw1h20 new_item = cdrdcs1 comp_item = comps01 slot = weapon creature = EVAL ~%cre%~ END
END
          
/////                                                  \\\\\
///// wrap it up and make it purty!                    \\\\\
/////                                                  \\\\\    

COPY_EXISTING ~rndtres.2da~ ~override~
  PRETTY_PRINT_2DA 
  
ACTION_IF include_hq AND FILE_EXISTS_IN_GAME ~cd5108.are~ BEGIN // swap items in extended gnoll stronghold

  INCLUDE ~iwdification/iwdspells/lib/alter_area-creature_item.tpa~
  COPY_EXISTING ~cd5108.are~ ~override~
    LPF ALTER_AREA_ITEM STR_VAR item = "cdhqsw2h" match_item = EVAL "%tutu_var%sw2h01" END
    BUT_ONLY
    
END
