// since this is meant to be run after both arcane & divine components, we cannot rely on 
// e.g. %WIZARD_MIND_BLANK% being set. so it's a bit tedious, but use spell.ids lookups instead

OUTER_SET NUM_a327 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_ICELANCE~)) - 2000)
OUTER_SET NUM_a427 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_EMOTION_COURAGE~)) - 2000)
OUTER_SET NUM_a428 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_EMOTION_FEAR~)) - 2000)
OUTER_SET NUM_a429 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_EMOTION_HOPE~)) - 2000)
OUTER_SET NUM_a524 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_SHROUD_OF_FLAME~)) - 2000)
OUTER_SET NUM_a610 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_ANTIMAGIC_SHELL~)) - 2000)
OUTER_SET NUM_a802 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_MIND_BLANK~)) - 2000)
OUTER_SET NUM_a814 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_IRON_BODY~)) - 2000)
OUTER_SET NUM_d324 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SPIKE_GROWTH~)) - 1000)
OUTER_SET NUM_d325 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CLOUDBURST~)) - 1000)
OUTER_SET NUM_d326 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_MOLD_TOUCH~)) - 1000)
OUTER_SET NUM_d419 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_PRODUCE_FIRE~)) - 1000)
OUTER_SET NUM_d420 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_STATIC_CHARGE~)) - 1000)
OUTER_SET NUM_d422 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_BLOOD_RAGE~)) - 1000)
OUTER_SET NUM_d423 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CLOUD_OF_PESTILENCE~)) - 1000)
OUTER_SET NUM_d424 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_UNFAILING_ENDURANCE~)) - 1000)
OUTER_SET NUM_d426 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SMASHING_WAVE~)) - 1000)
OUTER_SET NUM_d427 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_THORN_SPRAY~)) - 1000)
OUTER_SET NUM_d428 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_WALL_OF_MOONLIGHT~)) - 1000)
OUTER_SET NUM_d519 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SPIKE_STONES~)) - 1000)
OUTER_SET NUM_d615 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_ENTROPY_SHIELD~)) - 1000)
OUTER_SET NUM_d617 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_WHIRLWIND~)) - 1000)
OUTER_SET NUM_d716 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SYMBOL_HOPELESSNESS~)) - 1000)
OUTER_SET NUM_d734 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_DESTRUCTION~)) - 1000)
OUTER_SET NUM_d736 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_MIST_OF_ELDATH~)) - 1000)
  
OUTER_SET op_318 = 206
OUTER_SET op_321 = 206
ACTION_IF enhanced_edition BEGIN OUTER_SET op_318 = 318 OUTER_SET op_321 = 321 END 

COPY_EXISTING ~7eyes.2da~ ~override~
  PATCH_IF NUM_d716 > 0 BEGIN
    REPLACE_TEXTUALLY ~\(SPPR311[ %TAB%]+\)[^ %TAB%%LNL%%MNL%%WNL%]+~ ~\1sppr%NUM_d716%~
  END ELSE BEGIN  
    REPLACE_TEXTUALLY ~\(SPPR311[ %TAB%]+\)[^ %TAB%%LNL%%MNL%%WNL%]+~ ~\1*~
  END  
  PATCH_IF NUM_d734 > 0 BEGIN
    REPLACE_TEXTUALLY ~\(SPPR511[ %TAB%]+\)[^ %TAB%]+~ ~\1sppr%NUM_d734%~
  END ELSE BEGIN  
    REPLACE_TEXTUALLY ~\(SPPR511[ %TAB%]+\)[^ %TAB%]+~ ~\1*~
  END  
  BUT_ONLY IF_EXISTS
  
ACTION_IF ((NUM_a429 > 0) AND (NUM_d716 > 0)) BEGIN // emotion hope

  // symbol of hopelessness
  COPY_EXISTING ~spwi%NUM_a429%.spl~ ~override~
    LPF CLONE_EFFECT INT_VAR match_opcode = op_321 STR_VAR match_resource = spwi411 resource = EVAL ~sppr%NUM_d716%~ END
    BUT_ONLY

END
  
ACTION_IF NUM_a802 > 0 BEGIN // mind blank

  // blood rage, symbol hopelessness
  COPY_EXISTING ~spwi%NUM_a802%.spl~ ~override~ 
    PATCH_FOR_EACH var IN ~%NUM_d422%~ ~%NUM_d716%~ BEGIN               
      PATCH_IF var > 0 BEGIN 
        LPF CLONE_EFFECT INT_VAR match_opcode = op_318 STR_VAR match_resource = spwi813 resource = EVAL ~sppr%var%~ END
      END  
    END 
    BUT_ONLY   

END 
  
ACTION_IF NUM_a610 > 0 BEGIN // antimagic shell

  // mist of eldath, whirlwind, spike stones, wall of moonlight, thorn spray, smashing wave, produce fire, static charge, cloud of pestilence, mold touch, spike growth
  COPY_EXISTING ~spwi%NUM_a610%.spl~ ~override~ 
    PATCH_FOR_EACH var IN ~%NUM_d736%~ ~%NUM_d617%~ ~%NUM_d519%~ ~%NUM_d428%~ ~%NUM_d427%~ ~%NUM_d426%~ ~%NUM_d419%~ ~%NUM_d420%~ ~%NUM_d423%~ ~%NUM_d326%~ ~%NUM_d324%~ BEGIN               
      PATCH_IF var > 0 BEGIN 
        LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi911 resource = EVAL ~sppr%var%~ END
      END  
    END 
    BUT_ONLY   

END    
 
ACTION_IF NUM_a814 > 0 BEGIN // iron body
   
  // mold touch, cloud of pestilence, unfailing endurance, mist of eldath   
  COPY_EXISTING ~spwi%NUM_a814%.spl~ ~override~ // ee games
                ~ibody.itm~          ~override~ // obg2 games
    PATCH_FOR_EACH var IN ~%NUM_d326%~ ~%NUM_d423%~ ~%NUM_d424%~ ~%NUM_d736%~ BEGIN               
      PATCH_IF var > 0 BEGIN // use silent since one of the items copied won't be patched
        LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = op_318 STR_VAR match_resource = spwi812 resource = EVAL ~sppr%var%~ END 
      END  
    END 
    BUT_ONLY    
  
END  

ACTION_IF NUM_d422 > 0 BEGIN // blood rage

  // emotions courage, fear, hope
  COPY_EXISTING ~sppr%NUM_d422%.spl~ ~override~
    PATCH_FOR_EACH var IN ~%NUM_a427%~ ~%NUM_a428%~ ~%NUM_a429%~ BEGIN               
      PATCH_IF var > 0 BEGIN
        LPF CLONE_EFFECT INT_VAR match_opcode = op_318 STR_VAR match_resource = spwi411 resource = EVAL ~spwi%var%~ END 
      END  
    END 
    BUT_ONLY

END
  
ACTION_IF ((NUM_a524 > 0) AND (NUM_d325 > 0)) BEGIN // cloudburst prot/remove shroud of flame

  COPY_EXISTING ~sppr%NUM_d325%.spl~ ~override~
    PATCH_IF enhanced_edition BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 321 target = 2 power = 3 timing = 1 resist_dispel = 2 STR_VAR resource = EVAL ~spwi%NUM_a524%~ END
    END ELSE BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 206 target = 2 power = 3 duration = 6 resist_dispel = 2 STR_VAR resource = EVAL ~spwi%NUM_a524%~ END
    END
    BUT_ONLY

END

ACTION_IF ((NUM_a327 > 0) AND (NUM_d615 > 0)) BEGIN // entropy shield blocking icelance

  COPY_EXISTING ~sppr%NUM_d615%.spl~ ~override~
    LPF CLONE_EFFECT INT_VAR match_opcode = op_318 STR_VAR match_resource = sppr106 resource = EVAL ~spwi%NUM_a327%~ END
    BUT_ONLY

END